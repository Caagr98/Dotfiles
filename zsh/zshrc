#!/bin/zsh
# vim: foldmethod=marker
#{{{1 History
setopt histignorealldups
HISTSIZE=1000
SAVEHIST=1000
HISTFILE=~/.zsh_history

#{{{1 Utils
autoload -Uz compinit && compinit
autoload -Uz colors && colors
autoload -U zmv

setopt extendedglob interactivecomments

nowrap() {
	echoti rmam
	cat
	echoti smam
}
alias su="sudo $SHELL"
alias o=xdg-open

s=car0b1nius@muncher.se
win=/mnt/windows/Users/admusr
: ~win

#{{{1 CNF
command_not_found_handler() {
	python /usr/lib/command-not-found $1
	exit 127
}

#{{{1 Color
#{{{2 Man
export LESS_TERMCAP_mb=$(printf "$fg[red]")
export LESS_TERMCAP_md=$(printf "$fg[blue]")
export LESS_TERMCAP_us=$(printf "$fg[green]")
export LESS_TERMCAP_so=$(printf "")
export LESS_TERMCAP_me=$(printf "$reset_color")
export LESS_TERMCAP_se=$(printf "$reset_color")
export LESS_TERMCAP_ue=$(printf "$reset_color")

#{{{2 Maven
mvn() {
	=mvn $@ | sed \
		-e "s/^\[INFO]/${fg[blue]}${bold_color}[INFO]${reset_color}/" \
		-e "s/^\[WARNING]/${fg[yellow]}${bold_color}[WARNING]${reset_color}/" \
		-e "s/^\[ERROR]/${fg[red]}${bold_color}[ERROR]${reset_color}/"
}

#{{{2 ls
ls() {
	if [[ -t 1 ]]; then
		=ls -F -x --color $@
	else
		=ls $@
	fi
}
#{{{1 Ctrl-Z
ctrlz() {
	[[ $(jobs | wc -l) > 0 ]] && zle push-line && BUFFER="fg" && zle accept-line
}
zle -N ctrlz
bindkey "^Z" ctrlz

#{{{1 Syntax highlight
source ~/dot/zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)

typeset -A ZSH_HIGHLIGHT_STYLES
ZSH_HIGHLIGHT_STYLES[alias]='fg=magenta,bold'
ZSH_HIGHLIGHT_STYLES[path]='fg=cyan'
ZSH_HIGHLIGHT_STYLES[single-hyphen-option]='fg=blue'
ZSH_HIGHLIGHT_STYLES[double-hyphen-option]='fg=blue'

ZSH_HIGHLIGHT_MAXLENGTH=300

#{{{1 Completion
source ~/dot/zsh/zsh-completions/zsh-completions.plugin.zsh
unsetopt menu_complete
unsetopt flowcontrol
setopt auto_menu
setopt complete_in_word
setopt always_to_end
WORDCHARS=''
zmodload -i zsh/complist
eval "$(dircolors -b)"

expand-or-complete-with-dots() {
	print -Pn "%{%F{red}......%f%}" | nowrap

	zle expand-or-complete
	zle redisplay
}
zle -N expand-or-complete-with-dots
bindkey "^I" expand-or-complete-with-dots

#{{{2 zstyle stuff
zstyle ':completion:*' format 'Completing %d'
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
zstyle ':completion:*' matcher-list 'm:{a-zA-Z-_}={A-Za-z_-}' 'r:|.=*'
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true
zstyle ':completion:*:*:*:*:*' menu select
zstyle ':completion:*:*:*:*:processes' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'
zstyle ':completion:*:*:*:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'
zstyle ':completion:*:cd:*' tag-order local-directories directory-stack path-directories
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion::complete:*' cache-path $ZSH_CACHE_DIR
zstyle ':completion::complete:*' use-cache 1

zstyle ':completion:*:*:*:users' ignored-patterns \
	adm amanda apache at avahi avahi-autoipd beaglidx bin cacti canna \
	clamav daemon dbus distcache dnsmasq dovecot fax ftp games gdm \
	gkrellmd gopher hacluster haldaemon halt hsqldb ident junkbust kdm \
	ldap lp mail mailman mailnull man messagebus  mldonkey mysql nagios \
	named netdump news nfsnobody nobody nscd ntp nut nx obsrun openvpn \
	operator pcap polkitd postfix postgres privoxy pulse pvm quagga radvd \
	rpc rpcuser rpm rtkit scard shutdown squid sshd statd svn sync tftp \
	usbmux uucp vcsa wwwrun xfs '_*'
zstyle '*' single-ignored show

#{{{1 Prompt
source ~/dot/zsh/prompt.zsh
