#!/bin/zsh
#{{{1 History
setopt histignorealldups
HISTSIZE=1000000
SAVEHIST=1000000
HISTFILE=~/.zsh_history
#{{{1 Utils
autoload -Uz colors && colors
autoload -U zmv && alias zmv="noglob zmv"

setopt extendedglob interactivecomments autocd
setopt auto_continue
WORDCHARS='*?_-.[]~=&;!#$%^(){}<>' # exclude '/' for backward-kill-word
REPORTTIME=3 # report elapsed time (> 3 sec.)

nowrap() {
	echoti rmam
	cat
	echoti smam
}

title() {
	printf "\e]0;%s\a" $1
}

mkcd() {
	mkdir -p $1
	cd $1
}

locd() {
	locate $@ | while read f; do
		[[ -d $f ]] && echo $f
	done
}

alias locate="locate --basename"
alias sudo="sudo env HOME=$HOME"
alias su="sudo $SHELL"
alias git="git --no-pager"

alias o=xdg-open
alias \$= # Allows copying scripts with $ prompts

for c in cp rm chmod chown rename; do
	alias $c="$c -v"
done

s=car0b1nius@muncher.se
#{{{1 Color and formatting
export LESS_TERMCAP_mb=$(printf "$fg[red]")
export LESS_TERMCAP_md=$(printf "$fg[blue]")
export LESS_TERMCAP_us=$(printf "$fg[green]")
export LESS_TERMCAP_so=$(printf "")
export LESS_TERMCAP_me=$(printf "$reset_color")
export LESS_TERMCAP_se=$(printf "$reset_color")
export LESS_TERMCAP_ue=$(printf "$reset_color")

mvn() {
	=mvn $@ | sed \
		-e "s/^\[INFO]/${fg[blue]}${bold_color}&${reset_color}/i" \
		-e "s/^\[WARNING]/${fg[yellow]}${bold_color}&${reset_color}/i" \
		-e "s/^\[ERROR]/${fg[red]}${bold_color}&${reset_color}/i"
}

ls() {
	if [[ -t 1 ]]; then
		=ls --color $@
	else
		=ls $@
	fi
}

alias grep="grep --color"
alias pacaur="pacaur --color always"
#{{{1 Completion
autoload -Uz compinit && compinit
unsetopt menu_complete
unsetopt flowcontrol
setopt auto_menu
setopt complete_in_word
setopt always_to_end
WORDCHARS=''
zmodload -i zsh/complist
eval "$(dircolors -b)"

alias zstyle="noglob zstyle"
zstyle ':completion:*' format "%B%F{yellow}%U%d%f%b%u"
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' matcher-list 'm:{[:lower:]}={[:upper:]}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' list-prompt '%SAt %p: Hit TAB for more, or the character to insert%s'
zstyle ':completion:*' select-prompt '%SScrolling active: current selection at %p%s'
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true
zstyle ':completion:*' menu select
zstyle ':completion:*' list-separator '|'
zstyle ':completion:*' list-suffixes true
zstyle ':completion:*' ignore-parents parent pwd
zstyle ':completion:*' completer _complete _approximate _ignored
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*:complete:*' use-cache 1
zstyle ':completion:*:complete:*' cache-path ~/.zsh_cache
zstyle -e ':completion:*:approximate:*' max-errors 'reply=( $(( ($#PREFIX+$#SUFFIX)/3 )) )'

zstyle ':completion:*:original' list-colors "=*=$color[red];$color[bold]"
zstyle ':completion:*:processes' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'
zstyle ':completion:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'

compdef '_files -g "*"' play
compdef '_files -g "*.midi#"' timidity
compdef '_files -g "*.ly"' lilypond
zstyle ':completion:*:cd:*' tag-order local-directories directory-stack path-directories


zstyle ':completion:*:*:*:users' ignored-patterns \
	adm amanda apache avahi beaglidx bin cacti canna clamav daemon \
	dbus distcache dovecot fax ftp games gdm gkrellmd gopher \
	hacluster haldaemon halt hsqldb ident junkbust ldap lp mail \
	mailman mailnull mldonkey mysql nagios \
	named netdump news nfsnobody nobody nscd ntp nut nx openvpn \
	operator pcap postfix postgres privoxy pulse pvm quagga radvd \
	rpc rpcuser rpm shutdown squid sshd sync uucp vcsa xfs \
	systemd-*

#{{{1 Large custom scripts
source ~/dot/zsh/prompt.zsh
#{{{1 ZLE
bindkey -e
function ctrl-z() {
	[[ $(jobs | wc -l) > 0 ]] && {
		zle push-line
		BUFFER="fg"
		zle accept-line
	}
}
zle -N ctrl-z

function expand-with-dots() {
	print -Pn "%{%F{red}...%f%}" | nowrap
	zle expand-word
	zle redisplay
}
zle -N expand-with-dots

function complete-with-dots() {
	print -Pn "%{%F{red}...%f%}" | nowrap
	zle complete-word
	zle redisplay
}
zle -N complete-with-dots

function rationalise-dot() {
	if [[ $LBUFFER = *.. ]]; then
		LBUFFER+=/.
	fi
	zle self-insert
}
zle -N rationalise-dot

function nop() {}
zle -N nop

# Terminfo is wrong sometimes.
bindkey -- "$terminfo[kich1]" overwrite-mode
bindkey -- "$terminfo[kbs]"   backward-delete-char
bindkey -- "$terminfo[kdch1]" delete-char
bindkey -- "$terminfo[kcuu1]" up-line-or-search
bindkey -- "$terminfo[kcud1]" down-line-or-search
bindkey -- "$terminfo[kcub1]" backward-char
bindkey -- "$terminfo[kcuf1]" forward-char
bindkey -- "$terminfo[khome]" beginning-of-line
bindkey -- "$terminfo[kend]"  end-of-line
bindkey -- "$terminfo[kpp]"   nop
bindkey -- "$terminfo[knp]"   nop
bindkey "^[[2~"   overwrite-mode       # Insert
bindkey "^?"      backward-delete-char # Backspace
bindkey "^[^?"    backward-delete-word # A-Backspace
bindkey "^[[3~"   delete-char          # Delete
bindkey "^[[3;3~" delete-word          # A-Delete
bindkey "^[[A"    up-line-or-search    # Up
bindkey "^[[B"    down-line-or-search  # Down
bindkey "^[[C"    forward-char         # Right
bindkey "^[[D"    backward-char        # Left
bindkey "^[[F"    end-of-line          # End
bindkey "^[[H"    beginning-of-line    # Home
bindkey "^[[5~"   nop                  # PgUp
bindkey "^[[6~"   nop                  # PgDn
bindkey "^Z" ctrl-z
bindkey "^@" expand-with-dots
bindkey "^I" complete-with-dots
bindkey "." rationalise-dot

#{{{1 Syntax highlight
source /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)

typeset -A ZSH_HIGHLIGHT_STYLES
ZSH_HIGHLIGHT_STYLES[alias]='fg=magenta,bold'
ZSH_HIGHLIGHT_STYLES[path]='fg=cyan'
ZSH_HIGHLIGHT_STYLES[single-hyphen-option]='fg=blue'
ZSH_HIGHLIGHT_STYLES[double-hyphen-option]='fg=blue'

ZSH_HIGHLIGHT_MAXLENGTH=1000
