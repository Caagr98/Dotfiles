#!/bin/zsh
[[ -f ~/.profile ]] && source ~/.profile
autoload -Uz add-zsh-hook
zsh_path="$(print -P %N)"
zsh_path=$zsh_path:A:h

fpath=($zsh_path/comp $fpath)

#{{{1 History
setopt histignorealldups
HISTSIZE=1000000
SAVEHIST=1000000
HISTFILE=~/.zsh_history
#{{{1 Utils
autoload -Uz colors && colors
autoload -U zmv && alias zmv="noglob zmv"

setopt extendedglob interactivecomments autocd
setopt auto_continue noflowcontrol

WORDCHARS='*?_-.[]~=&;!#$%^(){}<>' # exclude '/' for backward-kill-word
REPORTTIME=3 # report elapsed time (> 3 sec.)

nowrap() {
	echoti rmam
	cat
	echoti smam
}

title() {
	printf "\e]0;%s\a" $1
}

mkcd() {
	mkdir -p $1
	cd $1
}

locd() {
	locate $@ | while read f; do
		[[ -d $f ]] && echo $f
	done
}

alias locate="locate --basename"
alias sudo="sudo env HOME=$HOME "
alias su="sudo $SHELL"
alias git="git --no-pager"

alias https='http --default-scheme=https' # HTTPie

alias o=xdg-open
alias \$= # Allows copying scripts with $ prompts

for c in cp rm mv chmod chown rename; do
	alias $c="$c -v"
done

s=car0b1nius@muncher.se

reentry_hook() {
	if [[ $(stat -c "%h" .) -eq 0 ]]; then
		if [[ -d $PWD ]]; then
			echo "PWD recreated"
			builtin cd .
		else
			echo "PWD nonexistent!"
		fi
	fi
}
add-zsh-hook preexec reentry_hook

#{{{1 Color and formatting
export LESS_TERMCAP_mb=$(printf "$fg[red]")
export LESS_TERMCAP_md=$(printf "$fg[blue]")
export LESS_TERMCAP_us=$(printf "$fg[green]")
export LESS_TERMCAP_so=$(printf "")
export LESS_TERMCAP_me=$(printf "$reset_color")
export LESS_TERMCAP_se=$(printf "$reset_color")
export LESS_TERMCAP_ue=$(printf "$reset_color")

eval "$(dircolors -b)"
alias ls="ls --color=auto -F"
alias grep="grep --color=auto"
#{{{1 Completion
autoload -Uz compinit && compinit
setopt complete_in_word
setopt always_to_end
zmodload -i zsh/complist

alias zstyle="noglob zstyle"
zstyle ':completion:*' format "%B%F{yellow}%U%d%f%b%u"
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' matcher-list 'm:{[:lower:]}={[:upper:]}'
zstyle ':completion:*' list-prompt '%SAt %p: Hit TAB for more, or the character to insert%s'
zstyle ':completion:*' select-prompt '%SScrolling active: current selection at %p%s'
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true
zstyle ':completion:*' menu select
zstyle ':completion:*' list-separator '|'
zstyle ':completion:*' list-suffixes true
zstyle ':completion:*' ignore-parents parent pwd
zstyle ':completion:*' completer _complete _approximate _ignored
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*:complete:*' use-cache 1
zstyle ':completion:*:complete:*' cache-path ~/.zsh_cache
zstyle -e ':completion:*:approximate:*' max-errors 'reply=( $(( ($#PREFIX+$#SUFFIX)/3 )) )'

zstyle ':completion:*:original' list-colors "=*=$color[red];$color[bold]"
zstyle ':completion:*:processes' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'
zstyle ':completion:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'

zstyle ':completion:*:*:*:users' ignored-patterns \
	adm amanda apache avahi beaglidx bin cacti canna clamav daemon dbus \
	distcache dovecot fax ftp games gdm gkrellmd gopher hacluster haldaemon \
	halt hsqldb ident junkbust ldap lp mail mailman mailnull mldonkey mysql \
	nagios named netdump news nfsnobody nobody nscd ntp nut nx openvpn \
	operator pcap postfix postgres privoxy pulse pvm quagga radvd rpc rpcuser \
	rpm shutdown squid sshd sync uucp vcsa xfs systemd-*

autoload -U regexp-replace
setopt re_match_pcre

#{{{1 Large custom scripts
source ~/dot/zsh/prompt.zsh

#{{{1 ZLE
bindkey -e
function ctrl-z() {
	[[ $(jobs | wc -l) > 0 ]] && {
		zle push-line
		BUFFER="fg"
		zle accept-line
	}
}
zle -N ctrl-z
bindkey "^Z" ctrl-z

function expand-with-dots() {
	print -Pn "%{%F{red}...%f%}" | nowrap
	zle expand-word
	zle redisplay
}
zle -N expand-with-dots
bindkey "^@" expand-with-dots

function complete-with-dots() {
	print -Pn "%{%F{red}...%f%}" | nowrap
	zle complete-word
	zle redisplay
}
zle -N complete-with-dots
bindkey "^I" complete-with-dots

function nop() {}
zle -N nop

bindkey "^[[2~"   overwrite-mode       # Insert
bindkey "^?"      backward-delete-char # Backspace
bindkey "^[^?"    backward-delete-word # A-Backspace
bindkey "^[[3~"   delete-char          # Delete
bindkey "^[[3;3~" delete-word          # A-Delete
bindkey "^[[A"    up-line-or-history   # Up
bindkey "^[[B"    down-line-or-history # Down
bindkey "^[[C"    forward-char         # Right
bindkey "^[[D"    backward-char        # Left
bindkey "^[[F"    end-of-line          # End
bindkey "^[[H"    beginning-of-line    # Home
bindkey "^[[5~"   nop                  # PgUp
bindkey "^[[6~"   nop                  # PgDn

#{{{1 Plugins

function plug() {
	source $zsh_path/plug/$1/*.plugin.zsh
}

plug history-search-multi-word
zstyle ":history-search-multi-word" page-size 25
zstyle ":history-search-multi-word" highlight-color "standout"

plug fast-syntax-highlighting

HIGHLIGHT=(
	"alias"    "fg=magenta,bold"
	"path"     "fg=cyan"
	"variable" "fg=green,bold"
)
typeset -gA HSMW_HIGHLIGHT_STYLES
typeset -gA FAST_HIGHLIGHT_STYLES
HSMW_HIGHLIGHT_STYLES+=(${(kv)HIGHLIGHT})
FAST_HIGHLIGHT_STYLES+=(${(kv)HIGHLIGHT})
